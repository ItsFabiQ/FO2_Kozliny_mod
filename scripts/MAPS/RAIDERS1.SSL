/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Raiders Map 1
        Location: Raiders
        Description: East Side

        Log:

           Created: July 28, 1998

           Updated:
*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\Raiders1.h"
#include "..\headers\updatmap.h"

// For Raiders Map from Safe flag.

#define NAME                    SCRIPT_RAIDERS1

#include "..\headers\command.h"
#include "..\headers\Raiders.h"
#include "..\headers\NewReno.h"

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;

procedure start begin
end

procedure map_enter_p_proc begin
   //set_map_exit_tiles(0,0);

   // Give the player the Raiders EXP.
   if( ( raiders_dead ) and
       ( map_var( MVAR_Gave_Raiders_Reward ) == 0 ) ) then begin
      set_map_var( MVAR_Gave_Raiders_Reward, 1 );
      display_msg( mstr( 102 ) );
      give_xp( EXP_RAIDERS_DESTROYED );
      inc_general_rep( REP_BONUS_RAIDERS_DESTROYED );
   end

   // Determine if the Player finds the secret entrance.
   // Condition #1 - Player has already found this entrance at some point.
   // Condition #2 - Found Map in Bishop's Safe.
   // Condition #3 - Successful roll_vs_skill( OUTDOORSMAN );
   if( map_first_run ) then begin
      if( ( ( raiders_secret_enterance ) or
            ( is_success( roll_vs_skill( dude_obj, SKILL_OUTDOORSMAN, -30 ) ) ) ) ) then begin
         set_global_var( GVAR_RAIDER_SECRET_ENTRANCE_KNOWN, 1 );
         override_map_start_hex(20508,1,0);
         display_msg( mstr(101) );
      end
      else
         display_msg( mstr(100) );
   end

   if( global_var(GVAR_PLAYER_GOT_CAR) != 0 ) then begin
      if ( car_current_town == AREA_RAIDERS ) then begin
         if ( map_var(MVAR_Made_Car) == 0 ) then begin
            // Since the player can arrive at either of 2 elevations on this map,
            // put the car on the appropriate entry point.
            set_map_var(MVAR_Made_Car,1);

            if( dude_elevation == 0 ) then begin
               set_global_var( GVAR_RAIDERS_CAR_ELEVATION, 0 );
               Create_Car(CAR_RAIDERS_CAMP_HEX_NORMAL, 0 )
            end
            else begin
               set_global_var( GVAR_RAIDERS_CAR_ELEVATION, 1 );
               Create_Car(CAR_RAIDERS_CAMP_HEX_SECRET, 1 )
            end
         end
         else begin
            // The player may have returned to the Raiders area with the car
            // without returning here WITHOUT it. That means, the code below for
            // Made_Car == 1 was never run, meaning the car is still positioned
            // where it was last time. The problem here is that the Raiders map
            // has 2 potential car placements depending on which map you're entering.
            // This means we have to delete the car from its current elevation
            // if the player is showing up on the other potential elevation and
            // recreate the car on the new elevation. Make sure the player isn't just
            // leaving the radier's caves either.
            if( ( global_var( GVAR_COMING_FROM_INSIDE_RAIDERS ) == 0 ) and
                ( global_var( GVAR_RAIDERS_CAR_ELEVATION ) != dude_elevation ) and
                ( global_var( GVAR_LEFT_CAR_AT_RAIDERS ) == 0 ) ) then begin
               if( global_var( GVAR_RAIDERS_CAR_ELEVATION ) == 0 ) then begin
                  Dest_Car( CAR_RAIDERS_CAMP_HEX_NORMAL, 0 )
                  set_global_var( GVAR_RAIDERS_CAR_ELEVATION, 1 );
                  Create_Car(CAR_RAIDERS_CAMP_HEX_SECRET, 1 )
               end
               else begin
                  Dest_Car( CAR_RAIDERS_CAMP_HEX_SECRET, 1 )
                  set_global_var( GVAR_RAIDERS_CAR_ELEVATION, 0 );
                  Create_Car(CAR_RAIDERS_CAMP_HEX_NORMAL, 0 )
               end
            end
         end
         // Since the car is here, set the global to assume the player's going
         // to leave it here.
         set_global_var( GVAR_LEFT_CAR_AT_RAIDERS, 1 );
      end
      else if ( map_var(MVAR_Made_Car) == 1 ) then begin
         set_map_var(MVAR_Made_Car,0);
         set_global_var( GVAR_LEFT_CAR_AT_RAIDERS, 0 );

         if( global_var( GVAR_RAIDERS_CAR_ELEVATION ) == 0 ) then begin
            Dest_Car( CAR_RAIDERS_CAMP_HEX_NORMAL, 0 )
         end
         else begin
            Dest_Car( CAR_RAIDERS_CAMP_HEX_SECRET, 1 )
         end
      end
      else
         set_global_var( GVAR_LEFT_CAR_AT_RAIDERS, 0 );
   end

   set_global_var( GVAR_COMING_FROM_INSIDE_RAIDERS, 0 );
   Lighting;
   set_global_var(GVAR_LOAD_MAP_INDEX,0);
end

procedure map_update_p_proc begin
   if( dude_elevation == 1 ) then
      set_global_var( GVAR_RAIDER_SECRET_ENTRANCE_KNOWN, 1 );

   Lighting;
end