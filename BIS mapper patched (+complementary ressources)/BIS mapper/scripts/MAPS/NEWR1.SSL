/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: NewR1
        Location: New Reno
        Description: Virgin Street

        Log:

           Created: April 27, 1998

           Updated:
*/

/* Include Files */
#include "..\headers\define.h"
#include "..\headers\NewR1.h"
#include "..\headers\updatmap.h"

#define NAME                    SCRIPT_NEWR1

#include "..\headers\command.h"
#include "..\headers\NewReno.h"

procedure start;
procedure map_enter_p_proc;
procedure map_exit_p_proc;
procedure map_update_p_proc;

export variable new_reno_temp_box;
export variable new_reno_salvatore_bar_box;
export variable new_reno_bishop_bar_box;
export variable new_reno_mordino_bar_box;
export variable new_reno_jules_box;
export variable new_reno_bats_paw_obj;
export variable new_reno_big_jesus_obj;
export variable new_reno_mordino_men_obj;
export variable new_reno_cats_paw_obj;

procedure start begin
end

#define newr1_lighting                    if (dude_elevation == 1) then          \
                                             Basement_Lighting;                  \
                                          else if (dude_elevation == 2) then     \
                                             Indoor_Lighting;                    \
                                          else                                   \
                                             Lighting

procedure map_enter_p_proc begin
variable critter;
   if (is_loading_game == false) then begin
      if (made_slave_guild_guy == false) then begin
         set_made_slave_guild_guy;
         if ((global_var(GVAR_SAVINE_PAYOUT) == 0) and (global_var(GVAR_SAVINELLI_STATUS) == 1)) then begin
            Critter:=create_object_sid(PID_GUN_THUG_MALE,0,0,SCRIPT_RCNORD);
            critter_attempt_placement(Critter,27507,0);
         end
      end
      if (map_first_run) then begin
         display_mstr(100);
      end

      if (car_is_now_being_stolen) then begin
         off_car_is_now_being_stolen;
         set_stolen_car(stolen_car_stolen);
      end
      if ((car_current_town == AREA_NEW_RENO) and (car_at_new_reno_1) and (car_is_now_being_stolen == false)) then begin
         if (map_var(MVAR_Made_Car) == 0) then begin
            debug_msg("making car");
            set_map_var(MVAR_Made_Car,1);
            Create_Car(CAR_NEW_RENO_HEX,CAR_NEW_RENO_ELEV)
         end else begin
            debug_msg("already made car");
         end
      end else if (map_var(MVAR_Made_Car) != 0) then begin
         debug_msg("destroying car");
         set_map_var(MVAR_Made_Car,0);
         Dest_Car(CAR_NEW_RENO_HEX,CAR_NEW_RENO_ELEV)
      end
   end
   set_map_exit_tiles(33107, 6105);
   newr1_lighting;
end

procedure map_exit_p_proc begin

end

procedure map_update_p_proc begin
   newr1_lighting;
end